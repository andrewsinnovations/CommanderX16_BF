10 PC = 0
20 SP = 1
30 MP = 1
40 DIM MEM(1000)
50 DIM STACK(255)
60 PRINT "ENTER PROGRAM, FOLLOWED BY AN ! TO EXECUTE IT (NO ECHO)"
70 P$ = ""
80 GET C$
90 IF C$ <> "" THEN GOTO 110
100 GOTO 80
110 IF C$ = "!" THEN GOTO 140
120 P$ = P$ + C$
130 GOTO 80
140 PRINT "RUNNING..."
150 PC = PC + 1
160 IF PC > LEN(P$) THEN END
170 C$ = MID$(P$,PC,1)
180 IF C$ = ">" THEN GOTO 270
190 IF C$ = "<" THEN GOTO 300
200 IF C$ = "+" THEN GOTO 330
210 IF C$ = "-" THEN GOTO 360
220 IF C$ = "," THEN GOTO 430
230 IF C$ = "." THEN GOTO 390
240 IF C$ = "[" THEN GOTO 490
250 IF C$ = "]" THEN GOTO 650
260 GOTO 150
270 REM INC MEM PTR
280 MP = MP + 1
290 GOTO 150
300 REM DEC MEM PTR
310 MP = MP - 1
320 GOTO 150
330 REM INC MEM
340 MEM(MP) = MEM(MP) + 1
350 GOTO 150
360 REM DEC MEM
370 MEM(MP) = MEM(MP) - 1
380 GOTO 150
390 REM PRINT CHAR
400 IF MEM(MP) >= 97 THEN PRINT CHR$(MEM(MP) - 32);
410 IF MEM(MP) < 97 THEN PRINT CHR$(MEM(MP));
420 GOTO 150
430 REM INPUT CHAR
440 GET IN$
450 IF IN$ <> "" THEN GOTO 330
460 GOTO 440
470 MEM(MP) = ASC(IN$)
480 GOTO 150
490 REM START LOOP
500 IF MEM(MP) = 0 THEN GOTO 540
510 STACK(SP) = PC
520 SP = SP + 1 
530 GOTO 150
540 LC = 0
550 PC = PC + 1
560 IF PC > LEN(P$) THEN PRINT "UNEXPECTED END OF PROGRAM" : END
570 C$ = MID$(P$,PC,1)
580 IF C$ <> "[" THEN GOTO 610
590 LC = LC + 1
600 GOTO 550
610 IF C$ <> "]" THEN GOTO 550
620 IF LC = 0 THEN GOTO 150
630 LC = LC - 1
640 GOTO 550
650 REM END LOOP
660 IF MEM(MP) = 0 THEN GOTO 690
670 PC = STACK(SP - 1) 
680 GOTO 150
690 SP = SP - 1
700 GOTO 150